apply plugin: 'com.android.library'

android {
    compileSdkVersion 28



    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
}
android.libraryVariants.all { variant ->
    // Task names.
    String variantName = "${variant.name.capitalize()}" // Like 'Debug'
    String deployTaskGroup = "plugin"
    String deployTaskName = "deploy${variantName}PluginArchive" // Like 'deployDebugPluginArchive'
    String dependencyTaskName = "assemble${variantName}" // Like 'assembleDebug'
    // Source.
    String sourceAARFolder = "${buildDir.getPath()}/outputs/aar/"
    String sourceAARName = "${project.name}-${variant.name}.aar"
    // Target.
    String targetAssetFolder = "LogcatToUnityUnity/Assets/Artees/Diagnostics/LogcatToUnity"
    String targetAARFolder = "${rootDir.getPath()}/../${targetAssetFolder}" // Navigate into 'Assets'
    String targetAARName = "LogcatToUnity.aar" // The form you ship your plugin
    // Create task.
    task(deployTaskName, dependsOn: dependencyTaskName, type: Copy) {
        from(sourceAARFolder)
        into(targetAARFolder)
        include(sourceAARName)
        rename(sourceAARName, targetAARName)
    }.group = deployTaskGroup
}
